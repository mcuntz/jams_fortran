!> \file mo_elemeffects.f90

!> \brief   Elementary Effects.

!> \details This module is calculating the Elementary effects of model parameters
!>           using parameter sets sampled by the Morris method.

!> \author Juliane Mai
!> \date Mar 2012

MODULE mo_elemeffects

  ! This module is calculating the Elementary effects of model parameters
  ! using parameter sets sampled by the Morris method.

  ! Written    Juliane Mai,   Mar 2012
  ! Modified

  ! License
  ! -------
  ! This file is part of the JAMS Fortran library.

  ! It is NOT released under the GNU Lesser General Public License, yet.

  ! If you use this routine, please contact Juliane Mai.

  ! Copyright 2012 Juliane Mai

  USE mo_kind,    ONLY: i4, sp, dp

  IMPLICIT NONE

  PUBLIC :: elemeffects            ! Elementary effects of model parameters

  ! ------------------------------------------------------------------

  !     NAME
  !         elemeffects

  !     PURPOSE
  !>        \brief   Elementary Effects of model parameter.
  !
  !>        \details Determine the Elementary Effects of model parameters using parameter sets sampled by
  !>                 the Morris method.

  !     CALLING SEQUENCE
  !         call elemeffects(modeloutput,para,changedpara,elemeffect,counter, &
  !                          modelstatus_in=modelstatus)

  !     INTENT(IN)
  !>        \param[in] "real(sp/dp) :: modeloutput(:)/modeloutput(:,:)"  modeloutput(i)/ modeloutput(i,:) is 
  !>                                                                     model output using parameter set \f$ i \f$
  !>        \param[in] "real(sp/dp) :: para(:,:)"                        array of parameters sets\n
  !>                                                                     parameter sets have to be a Morris sequence\n
  !>                                                                     values >= 0.0 and <= 1.0\n
  !>                                                                     size(para,1) number of sets\n
  !>                                                                     size(para,2) number of parameters\n
  !>        \param[in] "integer(i4) :: changedpara(size(para,1))"        vector of parameter changed
  !>                                                                     between parameter set \f$ i \f$ and \f$ i+1 \f$

  !     INTENT(INOUT)
  !         None

  !     INTENT(OUT)
  !>        \param[out] "real(sp/dp) :: elemeffect(size(para,2))"        elementary effect per parameter
  !>        \param[out] "integer(i4) :: counter(size(para,2))"           ith elementary effect is determined using counter(i)
  !>                                                                     model outputs

  !     INTENT(IN), OPTIONAL
  !>        \param[in] "logical, optional :: modelstatus_in(size(para,1))"  
  !>                                                                     array of status of the model, i.e. if parameter set
  !>                                                                     leads to valid model output\n
  !>                                                                     DEFAULT: .true. (parameter set valid)

  !     INTENT(INOUT), OPTIONAL
  !         None

  !     INTENT(OUT), OPTIONAL
  !         None

  !     RETURN
  !         None
  !
  !     RESTRICTIONS
  !>       \note Parameter set needs to be a Morris sequence \n
  !>             --> see test/test_mo_elemeffects/morris for MATLAB files and readme

  !     EXAMPLE
  !         ! para and changedpara can be generated by MATLAB code
  !         ! para needs to be scaled between 0.0 and 1.0
  !         para(1) = (/ 0.4, 0.8 /)
  !         para(2) = (/ 0.8, 0.8 /)
  !         para(3) = (/ 0.8, 0.2 /)
  !         changedpara = (/ 1, 2, 0 /)
  !         modeloutput = (/ 3.2, 4.0, 0.8 /)
  !         call elemeffects(modeloutput,para,changedpara,elemeffect,counter)
  !
  !         see also test/test_mo_elemeffects for detailled example

  !     LITERATURE
  !         Morris, M. D., 1991.
  !                Factorial sampling plans for preliminary computational experiments.
  !                Technometrics 33:161-174.
  !         Saltelli, A., M. Ratto, T. Andres, F. Campolongo, J. Cariboni, D. Gatelli, M. Saisana, and S. Tarantola, 2008.
  !                Global Sensitivity Analysis. The Primer.
  !                John Wiley & Sons Ltd.
  !         Campolongo, F., J. Cariboni, and A. Saltelli, 2007.
  !                An effective screening design for sensitivity analysis of large models.
  !                Environmental Modelling & Software 22:1509-1518.

  !     HISTORY
  !         Written,  Juliane Mai, March 2012
  INTERFACE elemeffects
     MODULE PROCEDURE elemeffects_0d_dp, elemeffects_0d_sp, &
          elemeffects_1d_dp, elemeffects_1d_sp
  END INTERFACE elemeffects

  ! ------------------------------------------------------------------

  PRIVATE

  ! ------------------------------------------------------------------

CONTAINS

  ! ------------------------------------------------------------------

  SUBROUTINE elemeffects_0d_dp(modeloutput_0d,para,changedpara,elemeffect,counter,modelstatus_in)

    IMPLICIT NONE

    REAL(DP),    DIMENSION(:),            INTENT(IN)    :: modeloutput_0d   ! vector of output the model
    !                                                                       ! generated with certain parameters
    REAL(DP),    DIMENSION(:,:),          INTENT(IN)    :: para             ! matrix of parameters to test
    !                                                                       ! parameter sets have to be a
    !                                                                       ! morris sequence
    !                                                                       ! values >= 0.0 and <= 1.0
    !                                                                       ! size(para,1) number of sets
    !                                                                       ! size(para,2) number of parameters
    INTEGER(I4), DIMENSION(size(para,1)), INTENT(IN)    :: changedpara      ! vector of parameter changed
    !                                                                       ! between parameter set i and i+1
    REAL(DP),    DIMENSION(size(para,2)), INTENT(OUT)   :: elemeffect       ! elementary effect of each parameter
    INTEGER(I4), DIMENSION(size(para,2)), INTENT(OUT)   :: counter          ! ith elementary effect is
    !                                                                       ! based on counter(i) values
    LOGICAL,     DIMENSION(:), OPTIONAL,  INTENT(IN)    :: modelstatus_in   ! vector of status of the model
    !                                                                       ! .true. if parameter set was valid

    ! local variables
    INTEGER(I4)                                   :: sets                   ! Number of parameter sets
    INTEGER(I4)                                   :: i, valid
    LOGICAL,  DIMENSION(size(modeloutput_0d,1))   :: modelstatus            ! default .true.

    sets = size(para,1)
    elemeffect = 0.0_dp
    counter    = 0_i4

    if (present(modelstatus_in)) then
       modelstatus = modelstatus_in
    else
       modelstatus = .true.
    end if

    valid = 0
    do i=1, sets
       ! only within a trajectory
       if (changedpara(i) .gt. 0_i4) then
          ! determine model value
          if ( modelstatus(i) .and. modelstatus(i+1) .and. &
               abs(para(i+1,changedpara(i))-para(i,changedpara(i))) .gt. epsilon(0.0_dp) ) then
             elemeffect(changedpara(i)) = elemeffect(changedpara(i)) &
                  + abs(                                             &
                  (modeloutput_0d(i+1)-modeloutput_0d(i))/           &
                  (para(i+1,changedpara(i))-para(i,changedpara(i)))  &
                  )
             counter(changedpara(i)) = counter(changedpara(i)) + 1_i4
          end if
       end if
       ! count valid parameter sets
       if (modelstatus(i)) then
          valid = valid + 1_i4
       end if
    end do

    elemeffect(:) = elemeffect(:)/max(real(counter(:),dp), 1.0_dp)

  END SUBROUTINE elemeffects_0d_dp

  SUBROUTINE elemeffects_0d_sp(modeloutput_0d,para,changedpara,elemeffect,counter,modelstatus_in)

    IMPLICIT NONE

    REAL(SP),    DIMENSION(:),            INTENT(IN)    :: modeloutput_0d   ! vector of output the model
    !                                                                       ! generated with certain parameters
    REAL(SP),    DIMENSION(:,:),          INTENT(IN)    :: para             ! matrix of parameters to test
    !                                                                       ! parameter sets have to be a
    !                                                                       ! morris sequence
    !                                                                       ! values >= 0.0 and <= 1.0
    !                                                                       ! size(para,1) number of sets
    !                                                                       ! size(para,2) number of parameters
    INTEGER(I4), DIMENSION(size(para,1)), INTENT(IN)    :: changedpara      ! vector of parameter changed
    !                                                                       ! between parameter set i and i+1
    REAL(SP),    DIMENSION(size(para,2)), INTENT(OUT)   :: elemeffect       ! elementary effect of each parameter
    INTEGER(I4), DIMENSION(size(para,2)), INTENT(OUT)   :: counter          ! ith elementary effect is
    !                                                                       ! based on counter(i) values
    LOGICAL,     DIMENSION(:), OPTIONAL,  INTENT(IN)    :: modelstatus_in   ! vector of status of the model
    !                                                                       ! .true. if parameter set was valid

    ! local variables
    INTEGER(I4)                                   :: sets                   ! Number of parameter sets
    INTEGER(I4)                                   :: i, valid
    LOGICAL,  DIMENSION(size(modeloutput_0d,1))   :: modelstatus            ! default .true.

    sets = size(para,1)
    elemeffect = 0.0_sp
    counter    = 0_i4

    if (present(modelstatus_in)) then
       modelstatus = modelstatus_in
    else
       modelstatus = .true.
    end if

    valid = 0
    do i=1, sets
       ! only within a trajectory
       if (changedpara(i) .gt. 0_i4) then
          ! determine model value
          if ( modelstatus(i) .and. modelstatus(i+1) .and. &
               abs(para(i+1,changedpara(i))-para(i,changedpara(i))) .gt. epsilon(0.0_sp) ) then
             elemeffect(changedpara(i)) = elemeffect(changedpara(i)) &
                  + abs(                                             &
                  (modeloutput_0d(i+1)-modeloutput_0d(i))/           &
                  (para(i+1,changedpara(i))-para(i,changedpara(i)))  &
                  )
             counter(changedpara(i)) = counter(changedpara(i)) + 1_i4
          end if
       end if
       ! count valid parameter sets
       if (modelstatus(i)) then
          valid = valid + 1_i4
       end if
    end do

    elemeffect(:) = elemeffect(:)/max(real(counter(:),sp), 1.0_sp)

  END SUBROUTINE elemeffects_0d_sp

  SUBROUTINE elemeffects_1d_dp(modeloutput_1d,para,changedpara,elemeffect,counter,modelstatus_in)

    IMPLICIT NONE

    REAL(DP),    DIMENSION(:,:),          INTENT(IN)    :: modeloutput_1d   ! matrix of output the model
    !                                                                       ! generated with certain parameters
    !                                                                       ! 1 row per parameter set
    REAL(DP),    DIMENSION(:,:),          INTENT(IN)    :: para             ! matrix of parameters to test
    !                                                                       ! parameter sets have to be a
    !                                                                       ! morris sequence
    !                                                                       ! values >= 0.0 and <= 1.0
    !                                                                       ! size(para,1) number of sets
    !                                                                       ! size(para,2) number of parameters
    INTEGER(I4), DIMENSION(size(para,1)), INTENT(IN)    :: changedpara      ! vector of parameter changed
    !                                                                       ! between parameter set i and i+1
    REAL(DP),    DIMENSION(size(para,2)), INTENT(OUT)   :: elemeffect       ! elementary effect of each parameter
    INTEGER(I4), DIMENSION(size(para,2)), INTENT(OUT)   :: counter          ! ith elementary effect is
    !                                                                       ! based on counter(i) values
    LOGICAL,     DIMENSION(:), OPTIONAL,  INTENT(IN)    :: modelstatus_in   ! vector of status of the model
    !                                                                       ! .true. if parameter set was valid

    ! local variables
    INTEGER(I4)                                   :: sets                   ! Number of parameter sets
    LOGICAL,  DIMENSION(size(modeloutput_1d,1))   :: modelstatus            ! default .true.
    INTEGER(I4)                                   :: i, valid, n

    if (present(modelstatus_in)) then
       modelstatus = modelstatus_in
    else
       modelstatus = .true.
    end if

    n    = size(modeloutput_1d,2)   ! number of model outputs per parameter set
    sets = size(para,1)
    elemeffect = 0.0_dp
    counter    = 0_i4

    valid = 0
    do i=1, sets
       ! only within a trajectory
       if (changedpara(i) .gt. 0_i4) then
          ! only if both both parameter sets were valid
          if (modelstatus(i) .and. modelstatus(i+1) .and. &
              abs(para(i+1,changedpara(i))-para(i,changedpara(i))) .gt. epsilon(1.0_dp) ) then
             elemeffect(changedpara(i)) = elemeffect(changedpara(i)) &
                  + sum( abs(                                        &
                  (modeloutput_1d(i+1,:)-modeloutput_1d(i,:))/       &
                  (para(i+1,changedpara(i))-para(i,changedpara(i)))  &
                  ) )
             counter(changedpara(i)) = counter(changedpara(i)) + n
          end if
       end if
       ! count valid parameter sets
       if (modelstatus(i)) then
          valid = valid + 1_i4
       end if
    end do

    elemeffect(:) = elemeffect(:)/max(real(counter(:),dp), 1.0_dp)
    counter(:)    = int(real(counter(:),dp)/real(n,dp),i4)

  END SUBROUTINE elemeffects_1d_dp

  SUBROUTINE elemeffects_1d_sp(modeloutput_1d,para,changedpara,elemeffect,counter,modelstatus_in)

    IMPLICIT NONE

    REAL(SP),    DIMENSION(:,:),          INTENT(IN)    :: modeloutput_1d   ! matrix of output the model
    !                                                                       ! generated with certain parameters
    !                                                                       ! 1 row per parameter set
    REAL(SP),    DIMENSION(:,:),          INTENT(IN)    :: para             ! matrix of parameters to test
    !                                                                       ! parameter sets have to be a
    !                                                                       ! morris sequence
    !                                                                       ! values >= 0.0 and <= 1.0
    !                                                                       ! size(para,1) number of sets
    !                                                                       ! size(para,2) number of parameters
    INTEGER(I4), DIMENSION(size(para,1)), INTENT(IN)    :: changedpara      ! vector of parameter changed
    !                                                                       ! between parameter set i and i+1
    REAL(SP),    DIMENSION(size(para,2)), INTENT(OUT)   :: elemeffect       ! elementary effect of each parameter
    INTEGER(I4), DIMENSION(size(para,2)), INTENT(OUT)   :: counter          ! ith elementary effect is
    !                                                                       ! based on counter(i) values
    LOGICAL,     DIMENSION(:), OPTIONAL,  INTENT(IN)    :: modelstatus_in   ! vector of status of the model
    !                                                                       ! .true. if parameter set was valid

    ! local variables
    INTEGER(I4)                                   :: sets                   ! Number of parameter sets
    LOGICAL,  DIMENSION(size(modeloutput_1d,1))   :: modelstatus            ! default .true.
    INTEGER(I4)                                   :: i, valid, n

    if (present(modelstatus_in)) then
       modelstatus = modelstatus_in
    else
       modelstatus = .true.
    end if

    n    = size(modeloutput_1d,2)   ! number of model outputs per parameter set
    sets = size(para,1)
    elemeffect = 0.0_sp
    counter    = 0_i4

    valid = 0
    do i=1, sets
       ! only within a trajectory
       if (changedpara(i) .gt. 0_i4) then
          ! only if both both parameter sets were valid
          if (modelstatus(i) .and. modelstatus(i+1) .and. &
              abs(para(i+1,changedpara(i))-para(i,changedpara(i))) .gt. epsilon(1.0_sp) ) then
             elemeffect(changedpara(i)) = elemeffect(changedpara(i)) &
                  + sum( abs(                                        &
                  (modeloutput_1d(i+1,:)-modeloutput_1d(i,:))/       &
                  (para(i+1,changedpara(i))-para(i,changedpara(i)))  &
                  ) )
             counter(changedpara(i)) = counter(changedpara(i)) + n
          end if
       end if
       ! count valid parameter sets
       if (modelstatus(i)) then
          valid = valid + 1_i4
       end if
    end do

    elemeffect(:) = elemeffect(:)/max(real(counter(:),sp), 1.0_sp)
    counter(:)    = int(real(counter(:),sp)/real(n,sp),i4)

  END SUBROUTINE elemeffects_1d_sp

  ! ------------------------------------------------------------------

END MODULE mo_elemeffects
